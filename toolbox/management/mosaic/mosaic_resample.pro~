PRO mosaic_resample, workspace_in, workspace_out_mosaic, workspace_out_resample, prefix_in,prefix_out,file_extension,pixel_size,band_pos,fill_value,data_type ,factors, start_year, end_year
  ENVI, /restore_base_save_files
  ENVI_BATCH_INIT, log_file = 'batch.txt', /NO_STATUS_WINDOW
  ;the spatial resolution for MODIS land surface product
  ;pixel_size = [926.6254331, 926.6254331]
  ;start the year loop
  FOR iyear = start_year, end_year, 1 DO BEGIN
    year_str = STRING(iyear, format = '(i04)')
    year_in = workspace_in+!backslash+year_str
    year_out1 = workspace_out_mosaic +!backslash+year_str
    year_out2 = workspace_out_resample + !backslash + year_str
    IF(FILE_TEST(year_out1) NE 1)THEN BEGIN
      FILE_MKDIR, year_out1
    ENDIF
    IF(FILE_TEST(year_out2) NE 1)THEN BEGIN
      FILE_MKDIR,  year_out2
    ENDIF
    filename_regex = prefix_in+year_str+'*'+file_extension
    match_files = FILE_SEARCH(year_in, filename_regex, /FULLY_QUALIFY_PATH, COUNT = file_count)
    in_fid = LON64ARR(file_count)
    pos = LON64ARR(1, file_count)
    use_see_through = INTARR(file_count)
    see_through_val = FLTARR(file_count)
    FOR f = 0, file_count-1, 1 DO BEGIN
      ENVI_OPEN_FILE, match_files[f], r_fid = fid
      ;ENVI_OPEN_DATA_FILE, match_files[f], /modis, r_fid=fid
      ;fid = HDF_SD_START(match_files[f], /read )
      IF (fid EQ -1) THEN BEGIN
        ;If invalid file,pass
        CONTINUE
      ENDIF ELSE BEGIN
        in_fid[f] = fid
        pos[*, f] = band_pos
        use_see_through[f] = 1
        see_through_val[f] = fill_value
      ENDELSE
    ENDFOR                  ;mosaicing loop
    out_name = year_out1+!backslash+prefix_out+year_str+'mosaic.tif'
    georef_mosaic_setup, fids = in_fid, out_ps = pixel_size, dims = dims, xsize = xsize, ysize = ysize, $
      x0 = x0, y0 = y0, map_info = map_info
    ;Start mosaic the images
    ENVI_DOIT, 'mosaic_doit', DIMS = dims, FID = in_fid, $
      OUT_DT = data_type, OUT_NAME = out_name, PIXEL_SIZE = pixel_size, $
      POS = pos, background = fill_value, $
      XSIZE = xsize, YSIZE = ysize, X0 = x0, Y0 = y0, $
      SEE_THROUGH_VAL = see_through_val, $
      USE_SEE_THROUGH = use_see_through, $
      R_FID = r_fid, $
      MAP_INFO = map_info, /GEOREF
    ;resample the result after the mosaic
    out_name2 =  year_out2+!backslash+ prefix_out+year_str+ 'resample.tif'
    ENVI_FILE_QUERY, r_fid, dims = dims, nb = nb
    ENVI_DOIT, 'resize_doit', $
      fid = r_fid, pos = 0, dims = dims, $
      interp = 0, rfact = [factors, factors], $
      out_name = out_name2
    ENVI_FILE_MNG, id = in_fid, /remove
    ENVI_FILE_MNG, id = r_fid, /remove
  ENDFOR
  PRINT, 'Finished'
  ENVI_BATCH_EXIT
END
